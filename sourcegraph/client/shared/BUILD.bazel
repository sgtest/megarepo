load("@aspect_rules_js//js:defs.bzl", "js_library", "js_run_binary")
load("//dev:defs.bzl", "sass_library")
load("//client/shared/dev:generate_schema.bzl", "generate_schema")
load("//client/shared/dev:generate_graphql_operations.bzl", "generate_graphql_operations")
load("//client/shared/dev:tools.bzl", "module_style_typings")

module_style_typings(
    name = "module_style_typings",
)

generate_graphql_operations(
    name = "graphql_operations",
    srcs = [
        ":graphql_operations_files",
    ],
    interface_name = "SharedGraphQlOperations",
    output = "src/graphql-operations.ts",
)

js_library(
    name = "graphql_operations_files",
    # Keep in sync with glob in client/shared/dev/generateGraphQlOperations.js
    srcs = glob(
        [
            "src/**/*.ts",
            "src/**/*.tsx",
        ],
        [
            "src/testing/**/*.*",
            "src/schema.ts",
            # TODO: Ignore legacy build generated file as it conflicts with the Bazel
            # build. This can be removed after the migration.
            "src/graphql-operations.ts",
        ],
    ),
    visibility = ["//client/shared:__pkg__"],
)

js_run_binary(
    name = "graphql_schema",
    outs = ["src/schema.ts"],
    args = [
        "src/schema.ts",
    ],
    chdir = package_name(),
    tool = "//client/shared/dev:generate_graphql_schema",
)

[generate_schema(
    name = name,
    out = "src/schema/%s.schema.d.ts" % name,
) for name in [
    "json-schema-draft-07",
    "site",
    "settings",
    "batch_spec",
]]

sass_library(
    name = "global-style-vars",
    srcs = [
        "src/global-styles/icons.scss",
    ],
    visibility = ["//visibility:public"],
)
