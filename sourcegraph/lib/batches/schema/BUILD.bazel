load("@io_bazel_rules_go//go:def.bzl", "go_library")
load("//dev:write_generated_to_source_files.bzl", "write_generated_to_source_files")

GENERATED_FILES = [
    "batch_spec_stringdata.go",
    "changeset_spec_stringdata.go",
]

# Telling gazelle to ignore here otherwise it wants to add generated files to srcs instead of fetching them
# gazelle:ignore
go_library(
    name = "schema",
    srcs = [
        ":generate_stringdata_code",
    ],
    importpath = "github.com/sourcegraph/sourcegraph/lib/batches/schema",
    visibility = ["//visibility:public"],
)

genrule(
    name = "generate_stringdata_code",
    srcs = [
        "//schema:batch_spec.schema.json",
        "//schema:changeset_spec.schema.json",
    ],
    outs = GENERATED_FILES,
    cmd = """
    srcs=($(SRCS))
    outs=($(OUTS))

    $(location //lib/batches/schema/gen:stringdata) -i $${srcs[0]} -name BatchSpecJSON -pkg schema -o $${outs[0]}
    $(location @go_sdk//:bin/gofmt) -s -w $${outs[0]}

    $(location //lib/batches/schema/gen:stringdata) -i $${srcs[1]} -name ChangesetSpecJSON -pkg schema -o $${outs[1]}
    $(location @go_sdk//:bin/gofmt) -s -w $${outs[1]}
    """,
    tools = [
        "//lib/batches/schema/gen:stringdata",
        "@go_sdk//:bin/gofmt",
    ],
    visibility = ["//visibility:public"],
)

write_generated_to_source_files(
    name = "write_generated_code",
    src = ":generate_stringdata_code",
    files = GENERATED_FILES,
    tags = ["go_generate"],
)
