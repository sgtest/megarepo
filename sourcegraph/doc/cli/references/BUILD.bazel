load("@aspect_bazel_lib//lib:directory_path.bzl", "make_directory_path")
load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")

filegroup(
    name = "doc_files",
    srcs = glob(
        ["**/*"],
        [
            "**/*.go",
            "*.sh",
        ],
    ),
    visibility = ["//doc:__pkg__"],
)

OUTPUT_FILES = [
    "admin.md",
    "api.md",
    "batch/apply.md",
    "batch/exec.md",
    "batch/index.md",
    "batch/new.md",
    "batch/preview.md",
    "batch/remote.md",
    "batch/repositories.md",
    "batch/validate.md",
    "code-intel/index.md",
    "code-intel/upload.md",
    "codeowners.md",
    "config/edit.md",
    "config/get.md",
    "config/index.md",
    "config/list.md",
    "debug.md",
    "extensions/copy.md",
    "extensions/delete.md",
    "extensions/get.md",
    "extensions/index.md",
    "extensions/list.md",
    "extensions/publish.md",
    "extsvc/create.md",
    "extsvc/edit.md",
    "extsvc/index.md",
    "extsvc/list.md",
    "index.md",
    "login.md",
    "lsif.md",
    "orgs/create.md",
    "orgs/delete.md",
    "orgs/get.md",
    "orgs/index.md",
    "orgs/list.md",
    "orgs/members/add.md",
    "orgs/members/index.md",
    "orgs/members/remove.md",
    "repos/add-metadata.md",
    "repos/delete-metadata.md",
    "repos/delete.md",
    "repos/get.md",
    "repos/index.md",
    "repos/list.md",
    "repos/update-metadata.md",
    "scout.md",
    "search.md",
    "serve-git.md",
    "snapshot.md",
    "teams.md",
    "users/create.md",
    "users/delete.md",
    "users/get.md",
    "users/index.md",
    "users/list.md",
    "users/prune.md",
    "users/tag.md",
    "validate.md",
    "version.md",
]

genrule(
    name = "generate_doc",
    outs = OUTPUT_FILES,
    cmd_bash = "GOMAXPROCS=8 USER=user HOME=/home/user $(execpath //dev/tools:src-cli) doc -o=$(RULEDIR)",
    tools = ["//dev/tools:src-cli"],
    visibility = ["//visibility:public"],
)

sh_test(
    name = "generate_doc_test",
    size = "small",
    srcs = ["generate_doc_test.sh"],
    args = [
        "$(location //dev/tools:src-cli)",
        "$(locations :generate_doc)",
    ],
    data = [
        ":generate_doc",
        "//dev/tools:src-cli",
    ],
)

# We use a copy_to_directory macro so write_source_files inputs and outputs are not at the same
# path, which enables the write_doc_files_diff_test to work.
copy_to_directory(
    name = "files_copy",
    srcs = [":generate_doc"],
)

write_source_files(
    name = "write_doc_files",
    files = {
        out: make_directory_path(
            out + "_directory_path",
            "files_copy",
            out,
        )
        for out in OUTPUT_FILES
    },
    suggested_update_target = "//dev:write_all_generated",
    tags = ["go_generate"],
    visibility = ["//visibility:public"],
)
