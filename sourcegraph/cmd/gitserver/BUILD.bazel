load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@container_structure_test//:defs.bzl", "container_structure_test")

go_library(
    name = "gitserver_lib",
    srcs = ["main.go"],
    importpath = "github.com/sourcegraph/sourcegraph/cmd/gitserver",
    visibility = ["//visibility:private"],
    x_defs = {
        "github.com/sourcegraph/sourcegraph/internal/version.version": "{STABLE_VERSION}",
        "github.com/sourcegraph/sourcegraph/internal/version.timestamp": "{VERSION_TIMESTAMP}",
    },
    deps = [
        "//cmd/gitserver/shared",
        "//cmd/sourcegraph-oss/osscmd",
        "//internal/sanitycheck",
    ],
)

go_binary(
    name = "gitserver",
    embed = [":gitserver_lib"],
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "tar_gitserver",
    srcs = [":gitserver"],
)

pkg_tar(
    name = "tar_p4_fusion_wrappers",
    srcs = [
        "p4-fusion-wrapper-detect-kill.sh",
        "process-stats-watcher.sh",
    ],
    package_dir = "/usr/local/bin",
    remap_paths = {
        "/p4-fusion-wrapper-detect-kill.sh": "/p4-fusion",
    },
    visibility = ["//visibility:public"],
)

oci_image(
    name = "image",
    base = "@wolfi_gitserver_base",
    entrypoint = [
        "/sbin/tini",
        "--",
        "/gitserver",
    ],
    tars = [
        ":tar_gitserver",
        ":tar_p4_fusion_wrappers",
    ],
    user = "sourcegraph",
    workdir = "/",
)

oci_tarball(
    name = "image_tarball",
    image = ":image",
    repo_tags = ["gitserver:candidate"],
)

container_structure_test(
    name = "image_test",
    timeout = "short",
    configs = ["image_test.yaml"],
    driver = "docker",
    image = ":image",
    tags = ["requires-network"],
)
