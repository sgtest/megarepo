/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
import org.elasticsearch.gradle.test.AntFixture

esplugin {
  description 'The Azure Repository plugin adds support for Azure storage repositories.'
  classname 'org.elasticsearch.repositories.azure.AzureRepositoryPlugin'
}

dependencies {
  compile 'com.microsoft.azure:azure-storage:5.0.0'
  compile 'com.microsoft.azure:azure-keyvault-core:0.8.0'
  compile 'org.apache.commons:commons-lang3:3.4'
}

dependencyLicenses {
  mapping from: /azure-.*/, to: 'azure'
  mapping from: /jackson-.*/, to: 'jackson'
  mapping from: /jersey-.*/, to: 'jersey'
  mapping from: /jaxb-.*/, to: 'jaxb'
  mapping from: /stax-.*/, to: 'stax'
}

thirdPartyAudit.excludes = [
  // Optional and not enabled by Elasticsearch
  'org.slf4j.Logger',
  'org.slf4j.LoggerFactory',
]

forbiddenApisTest {
  // we are using jdk-internal instead of jdk-non-portable to allow for com.sun.net.httpserver.* usage
  bundledSignatures -= 'jdk-non-portable'
  bundledSignatures += 'jdk-internal'
}

/** A task to start the fixture which emulates an Azure Storage service **/
task azureStorageFixture(type: AntFixture) {
  dependsOn compileTestJava
  env 'CLASSPATH', "${ -> project.sourceSets.test.runtimeClasspath.asPath }"
  executable = new File(project.runtimeJavaHome, 'bin/java')
  args 'org.elasticsearch.repositories.azure.AzureStorageFixture', baseDir, 'container_test'
}

integTestCluster {
  dependsOn azureStorageFixture

  keystoreSetting 'azure.client.integration_test.account', "azure_integration_test_account"
  /* The key is "azure_integration_test_key" encoded using base64 */
  keystoreSetting 'azure.client.integration_test.key', "YXp1cmVfaW50ZWdyYXRpb25fdGVzdF9rZXk="
  // Use a closure on the string to delay evaluation until tests are executed. The endpoint_suffix is used
  // in a hacky way to change the protocol and endpoint. We must fix that.
  setting 'azure.client.integration_test.endpoint_suffix',
          "ignored;DefaultEndpointsProtocol=http;BlobEndpoint=http://${ -> azureStorageFixture.addressAndPort }"
}