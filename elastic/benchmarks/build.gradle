import org.elasticsearch.gradle.info.BuildParams

/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */

apply plugin: 'elasticsearch.build'
apply plugin: 'application'
mainClassName = 'org.openjdk.jmh.Main'

tasks.named("assemble").configure { enabled = false }
archivesBaseName = 'elasticsearch-benchmarks'

tasks.named("test").configure { enabled = false }

configurations {
  expression
  painless
}

dependencies {
  api(project(":server")) {
    // JMH ships with the conflicting version 4.6. This prevents us from using jopt-simple in benchmarks (which should be ok) but allows
    // us to invoke the JMH uberjar as usual.
    exclude group: 'net.sf.jopt-simple', module: 'jopt-simple'
  }
  expression(project(path: ':modules:lang-expression', configuration: 'zip'))
  painless(project(path: ':modules:lang-painless', configuration: 'zip'))
  api "org.openjdk.jmh:jmh-core:$versions.jmh"
  annotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:$versions.jmh"
  // Dependencies of JMH
  runtimeOnly 'net.sf.jopt-simple:jopt-simple:4.6'
  runtimeOnly 'org.apache.commons:commons-math3:3.2'
}

// enable the JMH's BenchmarkProcessor to generate the final benchmark classes
// needs to be added separately otherwise Gradle will quote it and javac will fail
tasks.named("compileJava").configure {
  options.compilerArgs.addAll(["-processor", "org.openjdk.jmh.generators.BenchmarkProcessor"])
}

tasks.register('copyExpression', Copy) {
  dependsOn configurations.expression
  from { configurations.expression.collect { zipTree(it) } }
  into "${buildDir}/plugins/expression"
}

tasks.register("copyPainless", Copy) { 
  dependsOn configurations.painless
  from { configurations.painless.collect { zipTree(it) } }
  into "${buildDir}/plugins/painless"
}

tasks.named("run").configure {
  executable = "${BuildParams.runtimeJavaHome}/bin/java"
  jvmArgs << "-Dplugins.dir=${buildDir}/plugins" << "-Dtests.index=${buildDir}/index"
  dependsOn "copyExpression", "copyPainless"
}

// classes generated by JMH can use all sorts of forbidden APIs but we have no influence at all and cannot exclude these classes
disableTasks('forbiddenApisMain')

// No licenses for our benchmark deps (we don't ship benchmarks)
tasks.named("dependencyLicenses").configure { enabled = false }
tasks.named("dependenciesInfo").configure {  enabled = false }
tasks.named("dependenciesGraph").configure {  enabled = false }


tasks.named("thirdPartyAudit").configure {
  ignoreViolations(
          // these classes intentionally use JDK internal API (and this is ok since the project is maintained by Oracle employees)
          'org.openjdk.jmh.util.Utils'
  )
}

spotless {
  java {
    // IDEs can sometimes run annotation processors that leave files in
    // here, causing Spotless to complain. Even though this path ought not
    // to exist, exclude it anyway in order to avoid spurious failures.
    targetExclude 'src/main/generated/**/*.java'
  }
}
