/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */


import org.elasticsearch.gradle.Version
import org.elasticsearch.gradle.internal.info.BuildParams

apply plugin: 'elasticsearch.legacy-yaml-rest-test'

restResources {
  restTests {
    includeCore '*'
  }
}

File repo = file("$buildDir/testclusters/repo")
testClusters.matching { it.name == "yamlRestTest" }.configureEach {
  numberOfNodes = 2
  setting 'path.repo', repo.absolutePath
  // The first node does not have the ingest role so we're sure ingest requests are forwarded:
  nodes."yamlRestTest-0".setting 'node.roles', '[master,data,ml,remote_cluster_client,transform]'
}

testClusters.configureEach {
  setting 'xpack.security.enabled', 'false'
  requiresFeature 'es.index_mode_feature_flag_registered', Version.fromString("8.0.0")
}

tasks.named("yamlRestTest").configure {
  doFirst {
    project.delete(repo)
    repo.mkdirs()
  }
  systemProperty 'tests.rest.blacklist', [
    'cat.templates/10_basic/No templates',
    'cat.templates/10_basic/Sort templates',
    'cat.templates/10_basic/Multiple template',
  ].join(',')
}
