/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */
apply plugin: 'base'

configurations {
  apis
  freeTests
  platinumTests
}

dependencies {
  apis project(path: ':rest-api-spec', configuration: 'restSpecs')
  freeTests project(path: ':rest-api-spec', configuration: 'restTests')
  platinumTests project(path: ':x-pack:plugin', configuration: 'restXpackTests')
}

tasks.register('restResourcesZip', Zip).configure {
  description = 'Build archive containing all REST API specifications and YAML tests'

  destinationDirectory = layout.buildDirectory.dir('distributions')
  from(configurations.apis) {
    into 'rest-api-spec/api'
  }
  from(configurations.freeTests) {
    into 'rest-api-spec/test/free'
  }
  from(configurations.platinumTests) {
    into 'rest-api-spec/test/platinum'
  }
}

tasks.named("assemble").configure { dependsOn 'restResourcesZip' }
