import org.elasticsearch.gradle.MavenFilteringHack
import org.elasticsearch.gradle.info.BuildParams

apply plugin: 'elasticsearch.testclusters'
apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.rest-test'
apply plugin: 'elasticsearch.rest-resources'

dependencies {
  testImplementation project(':x-pack:qa')
}

def pluginPaths = project(':plugins').getChildProjects().findAll { pluginName, pluginProject ->
  // Do not attempt to install ingest-attachment in FIPS 140 as it is not supported (it depends on non-FIPS BouncyCastle)
  def ignoreIngestAttachment = BuildParams.inFipsJvm && pluginName == "ingest-attachment"

  // This plugin has to be configured to work via system properties
  def ignoreQuotaWareFs = pluginName == 'quota-aware-fs'

  return ignoreIngestAttachment == false && ignoreQuotaWareFs == false
}.collect {pluginName, pluginProject -> pluginProject.path }

ext.expansions = [
        'expected.plugins.count': pluginPaths.size()
]

processTestResources {
  inputs.properties(project.expansions)
  MavenFilteringHack.filter(it, expansions)
}

testClusters.matching { it.name == "integTest" }.configureEach {
  testDistribution = 'DEFAULT'
  setting 'xpack.security.enabled', 'true'
  setting 'xpack.license.self_generated.type', 'trial'
  user username: "test_user", password: "x-pack-test-password"

  pluginPaths.each {pluginPath ->
    plugin pluginPath
  }
}