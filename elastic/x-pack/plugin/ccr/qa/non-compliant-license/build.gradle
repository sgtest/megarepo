import org.elasticsearch.gradle.test.RestIntegTestTask

apply plugin: 'elasticsearch.testclusters'
apply plugin: 'elasticsearch.standalone-test'

dependencies {
  testImplementation(testArtifact(project(xpackModule('core'))))
  testImplementation project(xpackModule('ccr'))
  testImplementation project(':x-pack:plugin:ccr:qa:')
}

testClusters {
  'leader-cluster' {
    testDistribution = 'DEFAULT'
    setting 'xpack.security.enabled', 'true'
    user username: 'admin', password: 'admin-password', role: 'superuser'
  }

  'follow-cluster' {
    testDistribution = 'DEFAULT'
    setting 'xpack.license.self_generated.type', 'trial'
    setting 'xpack.security.enabled', 'true'
    user username: 'admin', password: 'admin-password', role: 'superuser'
    setting 'cluster.remote.leader_cluster.seeds',
            { "\"${testClusters.'leader-cluster'.getAllTransportPortURI().join(",")}\"" }
  }
}

tasks.register('leader-cluster', RestIntegTestTask) {
  mustRunAfter("precommit")
  systemProperty 'tests.target_cluster', 'leader'
}

tasks.register('follow-cluster', RestIntegTestTask) {
  dependsOn 'leader-cluster'
  useCluster testClusters.'leader-cluster'
  systemProperty 'tests.target_cluster', 'follow'
  nonInputProperties.systemProperty 'tests.leader_host',
    { "${testClusters.'follow-cluster'.getAllHttpSocketURI().get(0)}" }
}

tasks.named("check").configure { dependsOn "follow-cluster" }
tasks.named("test").configure { enabled = false }
