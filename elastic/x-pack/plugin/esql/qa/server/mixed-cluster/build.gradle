
import org.elasticsearch.gradle.Version
import org.elasticsearch.gradle.util.GradleUtils
import org.elasticsearch.gradle.internal.info.BuildParams
import org.elasticsearch.gradle.testclusters.StandaloneRestIntegTestTask

apply plugin: 'elasticsearch.internal-java-rest-test'
apply plugin: 'elasticsearch.internal-yaml-rest-test'
apply plugin: 'elasticsearch.internal-test-artifact'
apply plugin: 'elasticsearch.bwc-test'

restResources {
  restApi {
    include '_common', 'bulk', 'get', 'indices', 'esql', 'xpack', 'enrich', 'cluster'
  }
  restTests {
    includeXpack 'esql'
  }
}

dependencies {
  javaRestTestImplementation project(xpackModule('esql:qa:testFixtures'))
  javaRestTestImplementation project(xpackModule('esql:qa:server'))
}

GradleUtils.extendSourceSet(project, "javaRestTest", "yamlRestTest")

// ESQL is available in 8.11 or later
def supportedVersion = bwcVersion -> {
  return bwcVersion.onOrAfter(Version.fromString("8.11.0"));
}

// Versions on and after 8.13.3 will get a `version` parameter
def versionUnsupported = bwcVersion -> {
  return bwcVersion.before(Version.fromString("8.13.3"));
}

BuildParams.bwcVersions.withWireCompatible(supportedVersion) { bwcVersion, baseName ->
  def javaRestTest = tasks.register("v${bwcVersion}#javaRestTest", StandaloneRestIntegTestTask) {
    usesBwcDistribution(bwcVersion)
    systemProperty("tests.old_cluster_version", bwcVersion)
    systemProperty("tests.version_parameter_unsupported", versionUnsupported(bwcVersion))
    maxParallelForks = 1
  }

  def yamlRestTest = tasks.register("v${bwcVersion}#yamlRestTest", StandaloneRestIntegTestTask) {
    usesBwcDistribution(bwcVersion)
    systemProperty("tests.old_cluster_version", bwcVersion)
    systemProperty("tests.version_parameter_unsupported", versionUnsupported(bwcVersion))
    testClassesDirs = sourceSets.yamlRestTest.output.classesDirs
    classpath = sourceSets.yamlRestTest.runtimeClasspath
  }

  tasks.register(bwcTaskName(bwcVersion)) {
    dependsOn javaRestTest, yamlRestTest
  }
}

tasks.named("yamlRestTest") {
  enabled = false
}
