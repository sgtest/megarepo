/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

subprojects {
    /*
     * All subprojects are java projects using Elasticsearch's standard build
     * tools.
     */
    apply plugin: 'elasticsearch.build'

    /*
     * Subprojects may depend on the "core" lib but may not depend on any
     * other libs. This keeps are dependencies simpler.
     */
    project.afterEvaluate {
        configurations.all { Configuration conf ->
            dependencies.all { Dependency dep ->
                Project depProject = dependencyToProject(dep)
                if (depProject != null
                        && false == depProject.path.equals(':libs:core')
                        && false == isEclipse 
                        && depProject.path.startsWith(':libs')) {
                    throw new InvalidUserDataException("projects in :libs "
                            + "may not depend on other projects libs except "
                            + ":libs:core but "
                            + "${project.path} depends on ${depProject.path}")
                }
            }
        }
    }
}
