/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */

apply plugin: 'elasticsearch.build'
apply plugin: 'com.github.johnrengelman.shadow'

String jacksonVersion = "2.14.1"

dependencies {
  implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
}

['jarHell', 'thirdPartyAudit', 'forbiddenApisMain', 'splitPackagesAudit', 'checkstyleMain', 'licenseHeaders', 'spotlessJavaCheck'].each {
  tasks.named(it).configure {
    enabled = false
  }
}

pluginManager.withPlugin('com.diffplug.spotless') {
  spotless {
    java {
      targetExclude "src/main/java/com/fasterxml/jackson/core/filter/FilteringParserDelegate.java"
    }
  }
}

tasks.named("dependencyLicenses").configure {
  mapping from: /jackson-.*/, to: 'jackson'
}

shadowJar {
  exclude { element ->
    element.file == null && element.path.endsWith("FilteringParserDelegate.class")
  }
  manifest {
    attributes 'Multi-Release' : 'true'
  }
}
