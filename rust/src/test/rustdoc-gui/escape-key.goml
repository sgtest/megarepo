goto: file://|DOC_PATH|/test_docs/index.html
// First, we check that the search results are hidden when the Escape key is pressed.
write: (".search-input", "test")
wait-for: "#search > h1" // The search element is empty before the first search 
assert-attr: ("#search", {"class": "content"})
assert-attr: ("#main", {"class": "content hidden"})
press-key: "Escape"
assert-attr: ("#search", {"class": "content hidden"})
assert-attr: ("#main", {"class": "content"})

// Check that focusing the search input brings back the search results
focus: ".search-input"
assert-attr: ("#search", {"class": "content"})
assert-attr: ("#main", {"class": "content hidden"})

// Now let's check that when the help popup is displayed and we press Escape, it doesn't
// hide the search results too.
click: "#help-button"
assert-attr: ("#help", {"class": ""})
press-key: "Escape"
assert-attr: ("#help", {"class": "hidden"})
assert-attr: ("#search", {"class": "content"})
assert-attr: ("#main", {"class": "content hidden"})

// Check that Escape hides the search results when a search result is focused.
focus: ".search-input"
assert: ".search-input:focus"
press-key: "ArrowDown"
assert-false: ".search-input:focus"
assert: "#results a:focus"
press-key: "Escape"
assert-attr: ("#help", {"class": "hidden"})
assert-attr: ("#search", {"class": "content hidden"})
assert-attr: ("#main", {"class": "content"})
