// This test ensures that the correct style is applied to the rust logo in the sidebar.
goto: "file://" + |DOC_PATH| + "/test_docs/index.html"

define-function: (
    "check-logo",
    (theme, filter),
    [
        // Going to the doc page.
        ("goto", "file://" + |DOC_PATH| + "/test_docs/index.html"),
        // Changing theme.
        ("local-storage", {"rustdoc-theme": |theme|, "rustdoc-use-system-theme": "false"}),
        ("reload"),
        ("assert-css", (".rust-logo", {"filter": |filter|})),
        // Going to the source code page.
        ("goto", "file://" + |DOC_PATH| + "/src/test_docs/lib.rs.html"),
        // Changing theme (since it's local files, the local storage works by folder).
        ("local-storage", {"rustdoc-theme": |theme|, "rustdoc-use-system-theme": "false"}),
        ("reload"),
        ("assert-css", (".rust-logo", {"filter": |filter|})),
        // Now we check that the non-rust logos don't have a CSS filter set.
        ("goto", "file://" + |DOC_PATH| + "/huge_logo/index.html"),
        // Changing theme on the new page (again...).
        ("local-storage", {"rustdoc-theme": |theme|, "rustdoc-use-system-theme": "false"}),
        ("reload"),
        // Check there is no rust logo
        ("assert-false", ".rust-logo"),
        // Check there is no filter.
        ("assert-css", (".sidebar .logo-container img", {"filter": "none"})),
    ],
)

call-function: (
    "check-logo",
    ("ayu", "drop-shadow(rgb(255, 255, 255) 1px 0px 0px) drop-shadow(rgb(255, 255, 255) 0px 1px 0px) drop-shadow(rgb(255, 255, 255) -1px 0px 0px) drop-shadow(rgb(255, 255, 255) 0px -1px 0px)"),
)
call-function: (
    "check-logo",
    ("dark", "drop-shadow(rgb(255, 255, 255) 1px 0px 0px) drop-shadow(rgb(255, 255, 255) 0px 1px 0px) drop-shadow(rgb(255, 255, 255) -1px 0px 0px) drop-shadow(rgb(255, 255, 255) 0px -1px 0px)"),
)
call-function: (
    "check-logo",
    ("light", "none"),
)
